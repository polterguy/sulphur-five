
/*
 * Allows a root account to go through settings for app.
 */





/*
 * Retrieving settings from p5.data.
 */
select-data:x:/*/*/sulphur.settings





/*
 * Figuring out who can upload files.
 */
if:x:/@select-data/*/*/uploaders?value
  =:all

  /*
   * Everybody, including guests can upload files.
   */
  add:x:/../*/create-widgets/**/=uploaders/./**/=all
    src:checked

else-if:x:/@select-data/*/*/uploaders?value
  =:users

  /*
   * Only registered users can upload files.
   */
  add:x:/../*/create-widgets/**/=uploaders/./**/=users
    src:checked

else

  /*
   * Only root can upload files.
   */
  add:x:/../*/create-widgets/**/=uploaders/./**/=root
    src:checked





/*
 * Creates a modal widget allowing user to edit settings.
 */
create-widgets
  micro.widgets.modal:sulphur-modal-settings
    widgets
      h3
        innerValue:Settings
      micro.widgets.wizard-form:sulphur-settings
        p
          innerValue:Who can upload files to your server?
        radio-group
          .data-field:uploaders
          options
            Guests:all
            Registered users:users
            Only root:root
        div
          class:right
          widgets
            div
              class:strip
              widgets

                /*
                 * Save settings and close button.
                 */
                button
                  innerValue:Save
                  oninit

                    /*
                     * Setting focus to widget.
                     */
                    micro.page.set-focus:x:/../*/_event?value

                  onclick

                    /*
                     * Retrieving settings and updating.
                     */
                    micro.widgets.wizard-form.value:sulphur-settings
                    eval-x:x:/+/*/*/*
                    update-data:x:/*/*/sulphur.settings
                      src
                        sulphur.settings
                          uploaders:x:/@micro.widgets.wizard-form.value/*/uploaders?value

                    /*
                     * Deleting modal settings widget.
                     */
                    delete-widget:sulphur-modal-settings
