
/*
 * Allows a root account to go through settings for app.
 */





/*
 * Retrieving settings from p5.data.
 */
select-data:x:/*/*/sulphur.settings





/*
 * Figuring out who can upload files.
 */
if:x:/@select-data/*/*/uploaders?value
  =:all

  /*
   * Everybody, including guests can upload files.
   */
  add:x:/../*/create-widgets/**/=uploaders/./**/=all
    src:checked

else-if:x:/@select-data/*/*/uploaders?value
  =:users

  /*
   * Only registered users can upload files.
   */
  add:x:/../*/create-widgets/**/=uploaders/./**/=users
    src:checked

else

  /*
   * Only root can upload files.
   */
  add:x:/../*/create-widgets/**/=uploaders/./**/=root
    src:checked





/*
 * Figuring out who can view files.
 */
if:x:/@select-data/*/*/viewers?value
  =:reg-users

  /*
   * Any user can upload files.
   */
  add:x:/../*/create-widgets/**/=viewers/./**/=reg-users
    src:checked

else

  /*
   * Only root can upload files.
   */
  add:x:/../*/create-widgets/**/=viewers/./**/=guest
    src:checked




/*
 * Creates a modal widget allowing user to edit settings.
 */
create-widgets
  micro.widgets.modal:sulphur-modal-settings
    widgets
      h3
        innerValue:Settings
      micro.widgets.wizard-form:sulphur-settings
        p
          innerValue:Who can upload files to your server?
        radio-group
          .data-field:uploaders
          options
            Guests can upload files:all
            Any user can upload:users
            Only root can upload:root
        p
          innerValue:@"Who can view files on your server? Notice, if you set this to anybody, then also unregistered users 
will have access to view and download files, which basically means all your files are publicly available."
        radio-group
          .data-field:viewers
          options
            Anybody can view:guest
            Only users can view:reg-users
        div
          class:right
          widgets
            div
              class:strip
              style:"display:inline-block;"
              widgets
                button
                  innerValue:Save
                  style:"margin-bottom:0;"
                  oninit

                    /*
                     * Setting focus to widget.
                     */
                    micro.page.set-focus:x:/../*/_event?value

                  onclick

                    /*
                     * Retrieving settings and updating.
                     */
                    micro.widgets.wizard-form.value:sulphur-settings
                    eval-x:x:/+/*/*/*
                    update-data:x:/*/*/sulphur.settings
                      src
                        sulphur.settings
                          uploaders:x:/@micro.widgets.wizard-form.value/*/uploaders?value
                          viewers:x:/@micro.widgets.wizard-form.value/*/viewers?value

                    /*
                     * Deleting modal settings widget.
                     */
                    delete-widget:sulphur-modal-settings

                button
                  innerValue:Close
                  style:"margin-bottom:0;"
                  onclick

                    /*
                     * Simply deleting modal settings widget.
                     */
                    delete-widget:sulphur-modal-settings
