
/*
 * Creates the main display for Sulphur Five.
 */





/*
 * Checking settings to figure out who can upload files.
 *
 * This is necessary in order to be able to enable/disable uploading of files,
 * according to restrictions in settings of the app.
 */
select-data:x:/*/*/sulphur.settings
whoami
if:x:/@whoami/*/role?value
  =:root
  or:x:/@whoami/*/default?value
    not
    and:x:/@select-data/*/*/uploaders?value
      =:users

  /*
   * User is either root, or everybody is allowed to upload file.
   * Regardless, we include the necessary JavaScript to handle uploading of files.
   */
  p5.web.include-javascript:@"
p5.sulphur_upload_files = function(e) {
  var obsc = p5.$('sulphur-upload-obscurer').el;
  obsc.style.display = 'block';
  var files = e.target.files;
  for(var i = 0; i < files.length; i++) {
    var reader = new FileReader();
    reader.onload = (function(file) {
      return function(data) {
        p5.$(e.target.id).raise('.onupload', {
          onbefore:function(pars,evt) {
            pars.push(['sulphur-file-name', file.name], ['sulphur-file-content', data.target.result.split('base64,')[1]]);
          },
          onsuccess:function(ret,evt) {
            obsc.style.display = 'none';
          }
        });
      }
    })(files[i]);
    reader.readAsDataURL(files[i]);
  }
}"

else

  /*
   * User is not allowed to upload files, hence we remove all traces of logic
   * used for enabling uploading of files.
   *
   * This includes removing the uploader "obscurer", in addition to the actual
   * file input element, which encapsulates uploading of files, in addition to
   * the button that triggers our "browse for file" dialogue.
   */
  set:x:/../*/create-widget/*/widgets/*(/micro.widgets.obscurer|/**/button/=sulphur-upload-widget|/**/input/=sulphur-upload-file-input)





/*
 * Verifying user is "root", and if not, we remove settings.
 */
if:x:/@whoami/*/role?value
  !=:root

  /*
   * User is not root, hence we entirely remove the button that allows editing
   * of app's settings and configuration.
   */
  set:x:/../*/create-widget/**/button/=sulphur-settings-button





/*
 * Creating main display.
 */
create-widget
  parent:sulphur-main-container
  class:row
  widgets

    /*
     * Obscurer that obscures page during uploading of file(s).
     */
    micro.widgets.obscurer:sulphur-upload-obscurer
      message:Please wait while we're uploading your file ...
      style:"display:none;"

    /*
     * Wraps our search textbox and button.
     */
    div
      class:col-70
      widgets
        div
          class:fill strip
          widgets

            /*
             * Search textbox.
             */
            input:sulphur-search
              placeholder:Search ...
              type:text
              onkeydown:@"if (event.keyCode == 13) {p5.$('sulphur-search-button').raise('onclick');return false;}"
              oninit

                /*
                 * Setting initial focus to search textbox.
                 */
                micro.page.set-focus:x:/../*/_event?value

            /*
             * Search button.
             */
            button:sulphur-search-button
              innerValue:@"<span class=""icon-search""></span>"
              onclick

                /*
                 * Performing search.
                 */
                get-widget-property:sulphur-search
                  value
                eval-x:x:/+/*
                micro.evaluate.file:@SULPHUR/files/databind-files.hl
                  filter:x:/@get-widget-property/*/*?value

                /*
                 * Setting focus to search textbox again.
                 */
                micro.page.set-focus:sulphur-search

    /*
     * Wrapper for settings button, upload button, home button, and file input
     * element, that encapsulates uploading of files from the client.
     */
    div
      class:col-30
      widgets
        div
          class:right
          widgets

            /*
             * Hidden file input element, handled through JavaScript, that allows user
             * to upload files.
             */
            input:sulphur-upload-file-input
              style:"display:none;"
              type:file
              class:sulphurAttachmentFileInput
              onchange:"p5.sulphur_upload_files(event);"
              .onupload

                /*
                 * Retrieving the file name, and content, and base64 decoding 
                 * file's content, before we invoke file responsible for saving
                 * file, and inserting record into database.
                 */
                p5.web.post.get:sulphur-file-name
                p5.web.post.get:sulphur-file-content
                p5.string.decode-base64:x:/@p5.web.post.get/*/sulphur-file-content?value

                /*
                 * Invoking file responsible for saving file and inserting a record for it
                 * into our database.
                 */
                eval-x:x:/+/*
                micro.evaluate.file:@SULPHUR/handle-files/save-file.hl
                  filename:x:/../*/p5.web.post.get/*/sulphur-file-name?value
                  content:x:/@p5.string.decode-base64?value

                /*
                 * Starting editing of file.
                 */
                p5.web.get-location-url
                p5.web.set-location:{0}?edit={1}
                  :x:/@p5.web.get-location-url?value
                  :x:/@micro.evaluate.file?value

            div
              class:strip
              style:"display:inline-block;"
              widgets

                /*
                 * Settings button.
                 */
                button:sulphur-settings-button
                  innerValue:@"<span class=""icon-cog""></span>"
                  title:Manage settings ...
                  onclick

                    /*
                     * Launching settings form.
                     */
                    micro.evaluate.file:@SULPHUR/configuration/settings.hl

                /*
                 * Upload file button.
                 */
                button:sulphur-upload-widget
                  style:"padding-left:45px;padding-right:45px;"
                  innerValue:@"<span class=""icon-upload""></span>"
                  title:Upload file ...
                  onclick:@"document.querySelector('.sulphurAttachmentFileInput').click();return false;"

                /*
                 * Home button, leads user to main desktop.
                 */
                button
                  innerValue:@"<span class=""icon-home""></span>"
                  title:Return to desktop
                  onclick

                    /*
                     * Redirecting to root location.
                     */
                    p5.web.get-root-location
                    p5.web.set-location:x:/-?value

    div
      class:col-80 offset-10 air-top
      widgets

        /*
         * Datagrid wrapping actual files in system.
         */
        container:sulphur-files-grid
          class:row





/*
 * Initial databinding of files.
 */
micro.evaluate.file:@SULPHUR/files/databind-files.hl





/*
 * Checking to see if there is a "edit" argument, and if so, making sure we
 * display that file in a "modal edit container".
 */
p5.web.query.get:edit
if:x:/@p5.web.query.get/*?value

  /*
   * Opening database connection, and selecting file.
   */
  p5.mysql.connect:[sulphur]
    p5.mysql.select:@"select * from files where id = @id"
      @id:x:/@p5.web.query.get/*?value

    /*
     * Formatting values to display to user.
     */
    markdown2html:x:/@p5.mysql.select/*/*/description?value
    p5.types.date.format:x:/@p5.mysql.select/*/*/uploaded?value
      format:"d. MMM yyyy - HH:mm"

    /*
     * Displaying widget to user.
     */
    eval-x:x:/+/**/_id
    create-widget
      class:sulphur-file-editor
      onclick

        /*
         * Redirecting user to main Sulphur URL
         */
        p5.web.get-location-url
        p5.web.set-location:x:/-?value

      widgets
        container
          role:button
          class:sulphur-file-item sulphur-file-item-edit rounded shaded air-inner
          onclick:"event.stopPropagation(true);return false;"
          widgets
            h3
              innerValue:x:/@p5.mysql.select/*/*/name?value
            label
              class:sulphur-meta
              innerValue:{0} - {1}
                :x:/@p5.mysql.select/*/*/username?value
                :x:/@p5.types.date.format?value
            a
              href:#
              role:button
              class:sulphur-download-file
              innerValue:@"<span class=""icon-download""></span>"
              title:Download file ...
              onclick

                /*
                 * Downloading file.
                 */
                _id:x:/@p5.mysql.select/*/*/id?value
                p5.web.get-location-url
                p5.web.set-location:{0}?download={1}
                  :x:/@p5.web.get-location-url?value
                  :x:/@_id?value

            div
              innerValue:x:/@markdown2html?value
