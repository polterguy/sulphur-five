
/*
 * Creates the main display for Sulphur Five.
 */





/*
 * Clearing main container.
 */
clear-widget:sulphur-main-container





/*
 * Checking settings to figure out who can upload files.
 */
select-data:x:/*/*/sulphur.settings
whoami
if:x:/@whoami/*/role?value
  =:root
  or:x:/@whoami/*/default?value
    not
    and:x:/@select-data/*/*/uploaders?value
      =:users

  /*
   * Including JavaScript necessary to handle uploading of files.
   *
   * This is done in an Ajax request invoking [.onupload] on a input element,
   * on form which is actually hidden, and exclusively handled using JavaScript.
   */
  p5.web.include-javascript:@"
p5.sulphur_upload_files = function(e) {
  var obsc = p5.$('sulphur-upload-obscurer').el;
  obsc.style.display = 'block';
  var files = e.target.files;
  for(var i = 0; i < files.length; i++) {
    var reader = new FileReader();
    reader.onload = (function(file) {
      return function(data) {
        p5.$(e.target.id).raise('.onupload', {
          onbefore:function(pars,evt) {
            pars.push(['sulphur-file-name', file.name], ['sulphur-file-content', data.target.result.split('base64,')[1]]);
          },
          onsuccess:function(ret,evt) {
            obsc.style.display = 'none';
          }
        });
      }
    })(files[i]);
    reader.readAsDataURL(files[i]);
  }
}"

else

  /*
   * Only root can upload.
   */
  set:x:/../*/create-widget/*/widgets/*(/micro.widgets.obscurer|/div/*/widgets/*/div/*/widgets/*/div/*/widgets/*/button/=sulphur-upload-widget)





/*
 * Verifying user is "root", and if not, we remove settings.
 */
if:x:/@whoami/*/role?value
  !=:root
  set:x:/../*/create-widget/**/button/=sulphur-settings-button





/*
 * Creating main display.
 */
create-widget:sulphur-main-wrapper
  parent:sulphur-main-container
  class:row
  widgets
    micro.widgets.obscurer:sulphur-upload-obscurer
      message:Please wait while we're uploading your file ...
      style:"display:none;"
    div
      class:col-70
      widgets
        div
          class:fill strip
          widgets
            input:sulphur-search
              placeholder:Search ...
              type:text
              onkeydown:@"if (event.keyCode == 13) {p5.$('sulphur-search-button').raise('onclick');return false;}"
              oninit

                /*
                 * Setting initial focus to search textbox.
                 */
                micro.page.set-focus:x:/../*/_event?value

            button:sulphur-search-button
              innerValue:@"<span class=""icon-search""></span>"
              onclick

                /*
                 * Performing search.
                 */
                get-widget-property:sulphur-search
                  value
                eval-x:x:/+/*
                micro.evaluate.file:@SULPHUR/files/databind-files.hl
                  filter:x:/@get-widget-property/*/*?value

                /*
                 * Setting focus to search textbox again.
                 */
                micro.page.set-focus:sulphur-search

    div
      class:col-30
      widgets
        div
          class:right
          widgets

            /*
             * Hidden file input element, handled through JavaScript, that allows user
             * to add attachments to his email.
             */
            input:sulphur-upload-file-input
              style:"display:none;"
              type:file
              class:sulphurAttachmentFileInput
              onchange:"p5.sulphur_upload_files(event);"
              .onupload

                /*
                 * Retrieving the file name and content.
                 */
                p5.web.post.get:sulphur-file-name
                p5.web.post.get:sulphur-file-content

                /*
                 * Creating a random prefix, decoding vile's content, 
                 * and saving file to common private folder, to make sure 
                 * it's not accessible, even with a direct link.
                 */
                p5.string.decode-base64:x:/../*/p5.web.post.get/*/sulphur-file-content?value
                p5.types.guid.new
                save-file:/common/documents/private/{0}-{1}
                  :x:/@p5.types.guid.new?value
                  :x:/../*/p5.web.post.get/*/sulphur-file-name?value
                  src:x:/@p5.string.decode-base64?value

                /*
                 * Inserting entry for file into database.
                 */
                whoami
                p5.mysql.connect:[sulphur]
                  p5.mysql.insert:@"insert into files (filename, prefix, username, name, description) values(@filename, @prefix, @username, @name, @description)"
                    @filename:x:/../*/p5.web.post.get/*/sulphur-file-name?value
                    @prefix:x:/@p5.types.guid.new?value
                    @username:x:/@whoami/*/username?value
                    @name:"Default name, please change ..."
                    @description:"Default description, please change ..."

                /*
                 * Asking user to supply some description, and a name for file.
                 */
                eval-x:x:/+/*
                micro.evaluate.file:@SULPHUR/files/supply-file-info.hl
                  id:x:/@p5.mysql.connect/*/p5.mysql.insert/*/id?value

                /*
                 * Re-databinding grid, now with temporary name.
                 */
                micro.evaluate.file:@SULPHUR/files/databind-files.hl

            div
              class:strip
              style:"display:inline-block;"
              widgets

                /*
                 * Settings button.
                 */
                button:sulphur-settings-button
                  innerValue:@"<span class=""icon-cog""></span>"
                  title:Manage settings ...
                  onclick

                    /*
                     * Launching settings form.
                     */
                    micro.evaluate.file:@SULPHUR/configuration/settings.hl

                /*
                 * Upload file button.
                 */
                button:sulphur-upload-widget
                  style:"padding-left:45px;padding-right:45px;"
                  innerValue:@"<span class=""icon-upload""></span>"
                  title:Upload file ...
                  onclick:@"document.querySelector('.sulphurAttachmentFileInput').click();return false;"

                /*
                 * Home button, leads user to main desktop.
                 */
                button
                  innerValue:@"<span class=""icon-home""></span>"
                  title:Return to desktop
                  onclick

                    /*
                     * Redirecting to root location.
                     */
                    p5.web.get-root-location
                    p5.web.set-location:x:/-?value

    div
      class:col-100
      widgets

        /*
         * Datagrid wrapping actual files in system.
         */
        container:sulphur-files-grid
          class:row
          oninit

            /*
             * Initial databinding of files.
             */
            micro.evaluate.file:@SULPHUR/files/databind-files.hl





/*
 * Checking to see if there is a "preview" argument, and if so, making sure we
 * display that file in a "modal preview container".
 */
p5.web.query.get:preview
if:x:/@p5.web.query.get/*?value

  /*
   * Opening database connection, and selecting file.
   */
  p5.mysql.connect:[sulphur]
    p5.mysql.select:@"select * from files where id = @id"
      @id:x:/@p5.web.query.get/*?value

    /*
     * Formatting values to display to user.
     */
    markdown2html:x:/@p5.mysql.select/*/*/description?value
    p5.types.date.format:x:/@p5.mysql.select/*/*/uploaded?value
      format:"d. MMM yyyy - HH:mm"

    /*
     * Displaying widget to user.
     */
    eval-x:x:/+/**/_id
    create-widget
      class:sulphur-preview-file
      onclick

        /*
         * Redirecting user to main Sulphur URL
         */
        p5.web.get-location-url
        p5.web.set-location:x:/-?value

      widgets
        container
          role:button
          class:sulphur-file-item sulphur-file-item-preview rounded shaded air-inner
          widgets
            h3
              innerValue:x:/@p5.mysql.select/*/*/name?value
            label
              class:sulphur-meta
              innerValue:{0} - {1}
                :x:/@p5.mysql.select/*/*/username?value
                :x:/@p5.types.date.format?value
            a
              href:#
              role:button
              class:sulphur-download-file
              innerValue:@"<span class=""icon-download""></span>"
              title:Download file ...
              onclick

                /*
                 * Downloading file.
                 */
                _id:x:/@p5.mysql.select/*/*/id?value
                eval-x:x:/+/*
                micro.evaluate.file:@SULPHUR/handle-files/download-file.hl
                  id:x:/@_id?value

            div
              innerValue:x:/@markdown2html?value
