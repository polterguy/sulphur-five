
/*
 * File responsible for databinding files in the main grid.
 *
 * Requires no arguments, but you can optionally pass in [offset] and [filter] when invoking file,
 * to start out at a specific page, with a specified filter condition.
 */
.defaults
  offset:int:0





/*
 * Sanity checking optional arguments.
 */
micro.lambda.contract.optional:x:/..
  offset:long
  filter





/*
 * Selecting files user has access to according to [filter] and [offset].
 */
p5.mysql.connect:[sulphur]
  p5.mysql.select:@"select * from files order by id desc limit 10 offset @offset"
    @offset:x:(/../*/offset|/../*/.defaults/*/offset)/$?value.int

  /*
   * Looping through each result from above, and creating one record for each result.
   */
  for-each:x:/@p5.mysql.select/*
    markdown2html:x:/@_dp/#/*/description?value
    p5.types.date.format:x:/@_dp/#/*/uploaded?value
      format:"d. MMM yyyy - HH:mm"
    eval-x:x:/+/*/*/*/widgets/*/container/*/widgets/*(/h3|/label|/div)/*/innerValue|/+/**/_id
    add:x:/..p5.mysql.connect/*/create-widgets
      src
        container
          parent:sulphur-files-grid
          class:col-50
          widgets
            container
              role:button
              class:sulphur-file-item rounded shaded air-inner
              onclick

                /*
                 * Forward evaluated above.
                 * Invokes file responsible for handling file.
                 */
                _id:x:/@_dp/#/*/id?value
                eval-x:x:/+/*
                micro.evaluate.file:@SULPHUR/handle-files/handle-file.hl
                  id:x:/@_id?value

              widgets
                h3
                  innerValue:x:/@_dp/#/*/name?value
                label
                  class:sulphur-username
                  innerValue:x:/@_dp/#/*/username?value
                label
                  class:sulphur-uploaded
                  innerValue:x:/@p5.types.date.format?value
                a
                  href:#
                  role:button
                  class:sulphur-download-file
                  innerValue:@"<span class=""icon-download""></span>"
                  onclick

                    /*
                     * Downloading file.
                     */
                    _id:x:/@_dp/#/*/id?value
                    eval-x:x:/+/*
                    micro.evaluate.file:@SULPHUR/handle-files/download-file.hl
                      id:x:/@_id?value

                div
                  innerValue:x:/@markdown2html?value

  /*
   * Databinding files grid.
   */
  clear-widget:sulphur-files-grid
  create-widgets
