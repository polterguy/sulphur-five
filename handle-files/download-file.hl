
/*
 * Invoked when a file should be downloaded.
 *
 * Expects [sha] being Sha1 value of file.
 */





/*
 * First we need to figure out file's path, etc, by selecting its record from the database.
 */
p5.mysql.connect:[sulphur]
  p5.mysql.select:@"select * from files where sha = @sha"
    @sha:x:/../*/sha?value





/*
 * Verifying user has access to file.
 */
if:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/type?value
  !=:public

  /*
   * File is not publicly available.
   *
   * Now checking if this is a logged in user, and if not, we deny him to download
   * the file.
   */
  whoami
  if:x:/@whoami/*/default?value

    /*
     * User is not logged in, and file is not publicly available.
     */
    p5.web.response.set-status-code:403
    p5.web.echo:Access denied!
    return

  if:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/type?value
    =:private

    /*
     * File is private, verifying currently logged in user is the file's owner.
     */
    if:x:/@whoami/*/username?value
      !=:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/username?value

      /*
       * File is private, and the file does not belong to currently logged in user.
       */
      p5.web.response.set-status-code:403
      p5.web.echo:Access denied!
      return





/*
 * Then figuring out HTTP headers to set according to file's extension.
 */
split:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
  =:.
to-lower:x:/@split/0/-?name
load-file:@SULPHUR/configuration/mime-types.hl





/*
 * Setting "Content-Type" header.
 */
p5.web.header.set:Content-Type
  src:{0}/{1}
    :x:/@load-file/*/*/{0}/*?name
      :x:/@to-lower?value
    :x:/@load-file/*/*/{0}/*?value
      :x:/@to-lower?value





/*
 * Looping through all other HTTP headers according to configuration.
 */
for-each:x:/@load-file/*/*/{0}/*/*
  :x:/@to-lower?value
  p5.web.header.set:x:/@_dp/#?name
    src:x:/@_dp/#?value





/*
 * Making sure we set "Content-Disposition" header correctly.
 */
p5.web.header.set:Content-Disposition
  src:@"attachment; filename=""{0}"""
    :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value





/*
 * Making sure we set "ETag" header correctly.
 */
p5.web.header.set:ETag
  src:x:/../*/sha?value





/*
 * Figuring out if this is a "public", "private" or "protected" file,
 * which controls where we should be looking for it.
 */
_folder
switch:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/type?value
  case:private
    set:x:/@_folder?value
      src:~/documents/private/
  case:protected
    set:x:/@_folder?value
      src:/common/documents/private/
  case:public
    set:x:/@_folder?value
      src:/common/documents/public/





/*
 * Echoing file to caller.
 */
p5.web.echo-file:{0}{1}{2}
  :x:/@_folder?value
  :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
  :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
