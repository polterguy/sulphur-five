
/*
 * Edits a file.
 *
 * Expects [id] as the database ID for file to edit.
 */





/*
 * First we check if this is an initial upload by an anonymous guest, and if so,
 * we allow him to edit the file, but only once.
 *
 * This is necessary to allow a guest account to edit file initially, if the server
 * is setup to be an "anonymous dropbox file server".
 */
whoami
p5.web.session.get:sulphur.allow-edit
if:x:/@whoami/*/default?value
  and:x:/@p5.web.session.get/*?value
    not

  /*
   * User is guest, and file was not recently uploaded by current session.
   */
  micro.windows.info:Access denied!
    class:micro-windows-info warning
  return





/*
 * Making sure we delete session variable, which allows guests to initially
 * edit uploaded files, in case this is setup as an anonymous dropbox.
 */
p5.web.session.set:sulphur.allow-edit





/*
 * Opening database connection, and selecting file.
 */
p5.mysql.connect:[sulphur]
  p5.mysql.select:@"select * from files where id = @id"
    @id:x:/../*/id?value

  /*
   * Checking if file is a private guest file, and current user is root,
   * at which point we immediately transfer ownership of the file to the current
   * root account.
   */
  if:x:/@whoami/*/role?value
    =:root
    and:x:/@p5.mysql.select/*/*/username?value
      =:guest
    and:x:/@p5.mysql.select/*/*/type?value
      =:private

    /*
     * Giving current root user ownership of file, and physically moving it on
     * disc to reflect ownership change.
     */
    p5.mysql.update:@"update files set username = @username where id = @id"
      @username:x:/@whoami/*/username?value
      @id:x:/../*/id?value
    set:x:/@p5.mysql.select/*/*/username?value
      src:x:/@whoami/*/username?value
    move-file:/common/documents/private/{0}-{1}
      :x:/@p5.mysql.select/*/*/prefix?value
      :x:/@p5.mysql.select/*/*/filename?value
      dest:~/documents/private/{0}-{1}
        :x:/@p5.mysql.select/*/*/prefix?value
        :x:/@p5.mysql.select/*/*/filename?value

    /*
     * Giving the user some visual feedback about transfer of ownership.
     */
    micro.windows.info:File's ownership was transferred to you
      class:micro-windows-info success

  /*
   * Checking if user is allowed to edit file.
   */
  if:x:/@whoami/*/role?value
    !=:root
    and:x:/@whoami/*/username?value
      !=:x:/@p5.mysql.select/*/*/username?value

    /*
     * User is not root, and he tried to edit a file which he did not own.
     */
    micro.windows.info:Sorry, you're not allowed to edit this file
      class:micro-windows-info warning

    /*
     * Returning early to avoid user being allowed to edit file.
     */
    return

  /*
   * Formatting values to display to user.
   */
  p5.types.date.format:x:/@p5.mysql.select/*/*/uploaded?value
    format:"d. MMM yyyy - HH:mm"

  /*
   * Displaying widget to user.
   */
  eval-x:x:/+/**/_id
  create-widget
    class:sulphur-file-editor
    widgets
      container
        class:sulphur-file-item sulphur-file-item-edit rounded shaded air-inner
        widgets

          /*
           * Name textbox.
           */
          input
            type:text
            class:fill
            value:x:/@p5.mysql.select/*/*/name?value
            placeholder:Name of file ...
            title:Name for file
            oninit

              /*
               * Setting initial focus to "name" textbox.
               */
              micro.page.set-focus:x:/../*/_event?value

            onchange

              /*
               * Updates file's name in database.
               */
              _id:x:/@p5.mysql.select/*/*/id?value
              get-widget-property:x:/../*/_event?value
                value
              p5.mysql.connect:[sulphur]
                p5.mysql.update:@"update files set name = @name where id = @id"
                  @name:x:/@get-widget-property/*/*?value
                  @id:x:/@_id?value

          /*
           * Description textarea widget.
           */
          textarea
            placeholder:Description of file ...
            class:fill
            innerValue:x:/@p5.mysql.select/*/*/description?value
            rows:7
            title:Description for file, feel free to use Markdown here
            style:"margin-bottom:3rem;"
            onchange

              /*
               * Updates file's description in database.
               */
              _id:x:/@p5.mysql.select/*/*/id?value
              get-widget-property:x:/../*/_event?value
                value
              p5.mysql.connect:[sulphur]
                p5.mysql.update:@"update files set description = @description where id = @id"
                  @description:x:/@get-widget-property/*/*?value
                  @id:x:/@_id?value


          /*
           * Protection for file wrapper widget.
           */
          div
            style:"margin-bottom:2rem;margin-top:-2rem;"
            oninit

              /*
               * Checking file's protection, and setting radio buttons
               * initial values accordingly.
               *
               * Notice, in case guest uploads are enabled, and user is a guest,
               * we entirely delete this widget.
               *
               * This allows for an "anonymous WikiLeaks type of DropBox".
               */
              whoami
              if:x:/-/*/default?value

                /*
                 * User is guest, deleting widget and returning early.
                 */
                delete-widget:x:/../*/_event?value
                return

              _id:x:/@p5.mysql.select/*/*/id?value
              p5.mysql.connect:[sulphur]
                p5.mysql.scalar:@"select type from files where id = @id"
                  @id:x:/@_id?value
                switch:x:/@p5.mysql.scalar?value
                  case:private
                    set-widget-property:sulphur-rdo-private
                      checked
                  case:protected
                    set-widget-property:sulphur-rdo-protected
                      checked
                  case:public
                    set-widget-property:sulphur-rdo-public
                      checked

            events

              /*
               * Changes file's accessibility.
               *
               * Expected [protection] being either "private", "protected" or "public".
               */
              sulphur.edit-file.set-protection

                /*
                 * Sanity check.
                 */
                micro.lambda.contract.min:x:/..
                  protection:string

                /*
                 * updating file's protection in database.
                 */
                _id:x:/@p5.mysql.select/*/*/id?value
                p5.mysql.connect:[sulphur]
                  p5.mysql.select:@"select * from files where id = @id"
                    @id:x:/@_id?value
                  p5.mysql.update:@"update files set type = @protection where id = @id"
                    @protection:x:/../*/protection?value
                    @id:x:/@_id?value

                /*
                 * Physically moving file on disc.
                 *
                 * Notice, we need to figure out where the file previously physically
                 * was on disc, which depends upon file's previous protection level.
                 * For then to figure out file's new location, which depends upon
                 * its new protection level.
                 */
                _src
                _dest
                switch:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/type?value
                  case:private
                    set:x:/@_src?value
                      src:~/documents/private/{0}-{1}
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
                  case:protected
                    set:x:/@_src?value
                      src:/common/documents/private/{0}-{1}
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
                  case:public
                    set:x:/@_src?value
                      src:/common/documents/public/{0}-{1}
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
                switch:x:/../*/protection?value
                  case:private
                    set:x:/@_dest?value
                      src:~/documents/private/{0}-{1}
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
                  case:protected
                    set:x:/@_dest?value
                      src:/common/documents/private/{0}-{1}
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
                  case:public
                    set:x:/@_dest?value
                      src:/common/documents/public/{0}-{1}
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                        :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value

                /*
                 * Now we know file's previous location, in addition to its
                 * new location, and can move onwards with physically moving 
                 * the file on disc.
                 */
                move-file:x:/@_src?value
                  dest:x:/@_dest?value

            widgets
              p
                innerValue:@"Public means accessible to anyone, protected means only accessible for registered users, and private means only accessible for the file's owner."
              label
                for:sulphur-rdo-private
                innerValue:Private
              input:sulphur-rdo-private
                type:radio
                name:sulphur-rdo-protection
                onchange

                  /*
                   * Doing database update.
                   */
                  sulphur.edit-file.set-protection
                    protection:private

              label
                for:sulphur-rdo-protected
                innerValue:Protected
              input:sulphur-rdo-protected
                type:radio
                name:sulphur-rdo-protection
                onchange

                  /*
                   * Doing database update.
                   */
                  sulphur.edit-file.set-protection
                    protection:protected

              label
                for:sulphur-rdo-public
                innerValue:Public
              input:sulphur-rdo-public
                type:radio
                name:sulphur-rdo-protection
                onchange

                  /*
                   * Doing database update.
                   */
                  sulphur.edit-file.set-protection
                    protection:public

          /*
           * Close button.
           */
          a
            class:sulphur-close-file-editor
            innerValue:@"<span class=""icon-close""></span>"
            title:Close editor ...
            oninit

              /*
               * Closing window by redirecting to main app's root URL.
               */
              _id:x:/@p5.mysql.select/*/*/id?value
              p5.web.get-location-url
              set-widget-property:x:/../*/_event?value
                href:x:/@p5.web.get-location-url?value

          /*
           * Delete file button.
           */
          a
            href:#
            class:sulphur-delete-file-editor
            innerValue:@"<span class=""icon-trash""></span>"
            title:Delete file ...
            onclick

              /*
               * Deleting file and redirecting to main app's root URL.
               */
              _id:x:/@p5.mysql.select/*/*/id?value
              p5.mysql.connect:[sulphur]
                p5.mysql.transaction.begin
                  p5.mysql.select:@"select username, prefix, filename, type from files where id = @id"
                    @id:x:/@_id?value
                  p5.mysql.delete:@"delete from files where id = @id"
                    @id:x:/@_id?value

                  /*
                   * Figuring out file's folder.
                   */
                  _folder
                  if:x:/@p5.mysql.select/*/*/username?value
                    =:guest
                    and:x:/@p5.mysql.select/*/*/type?value
                      =:private

                    /*
                     * Privately owned guest file.
                     */
                    set:x:/@_folder?value
                      src:/common/documents/private/

                  else-if:x:/@p5.mysql.select/*/*/type?value
                    =:private

                    /*
                     * Privately owned user file.
                     */
                    set:x:/@_folder?value
                      src:/users/{0}/documents/private/
                        :x:/@p5.mysql.select/*/*/username?value

                  else-if:x:/@p5.mysql.select/*/*/type?value
                    =:protected

                    /*
                     * Protected user file.
                     */
                    set:x:/@_folder?value
                      src:/common/documents/private/

                  else-if:x:/@p5.mysql.select/*/*/type?value
                    =:public

                    /*
                     * Public user file.
                     */
                    set:x:/@_folder?value
                      src:/common/documents/public/

                  delete-file:{0}{1}-{2}
                    :x:/@_folder?value
                    :x:/@p5.mysql.select/*/*/prefix?value
                    :x:/@p5.mysql.select/*/*/filename?value

                  /*
                   * Committing transaction.
                   */
                  p5.mysql.transaction.commit

                  /*
                   * Reidrecting to app's main root URL.
                   */
                  p5.web.get-location-url
                  p5.web.set-location:x:/-?value
