
/*
 * Edits a file.
 *
 * Expects [id] as the database ID for file to edit.
 */





/*
 * Checking if this is a guest, at which point we simply return immediately,
 * since guests are never allowed to edit files.
 */
whoami
if:x:/@whoami/*/default?value

  /*
   * User is guest.
   */
  micro.windows.info:Access denied!
    class:micro-windows-info warning
  return





/*
 * Opening database connection, and selecting file.
 */
p5.mysql.connect:[sulphur]
  p5.mysql.select:@"select * from files where id = @id"
    @id:x:/../*/id?value

  /*
   * Checking if user is allowed to edit file.
   */
  if:x:/@whoami/*/role?value
    !=:root
    and:x:/@whoami/*/username?value
      !=:x:/@p5.mysql.select/*/*/username?value

    /*
     * User is not root, and he tried to edit a file which he did not own.
     */
    micro.windows.info:Sorry, you're not allowed to edit this file
      class:micro-windows-info warning

    /*
     * Returning early to avoid user being allowed to edit file.
     */
    return

  /*
   * Whenever a root account edits a file, he takes control over the file,
   * to allow a root account to delete and manage shared files, among other things.
   */
  if:x:/@whoami/*/role?value
    =:root
    and:x:/@p5.mysql.select/*/*/username?value
      !=:x:/@whoami/*/username?value

    /*
     * Figuring out file's folder, and moving it accordingly.
     *
     * Defaulting to "guest/private".
     */
    _folder:/common/documents/private/
    if:x:/@p5.mysql.select/*/*/username?value
      !=:guest

      /*
       * Figuring out file type, which can only be "public" or "protected".
       */
      switch:x:/@p5.mysql.select/*/*/type?value
        case:protected
          set:x:/@_folder?value
            src:/common/documents/private/
        case:public
          set:x:/@_folder?value
            src:/users/{0}/documents/public/
              :x:/@p5.mysql.select/*/*/username?value

    /*
     * Now we can actually physically move file.
     */
    move-file:{0}{1}{2}
      :x:/@_folder?value
      :x:/@p5.mysql.select/*/*/prefix?value
      :x:/@p5.mysql.select/*/*/filename?value
      dest:~/documents/private/{0}{1}
        :x:/@p5.mysql.select/*/*/prefix?value
        :x:/@p5.mysql.select/*/*/filename?value

    /*
     * Giving current root user ownership of file, and physically moving it on
     * disc to reflect ownership change.
     */
    p5.mysql.update:@"update files set username = @username, type='private' where id = @id"
      @username:x:/@whoami/*/username?value
      @id:x:/../*/id?value
    set:x:/@p5.mysql.select/*/*/username?value
      src:x:/@whoami/*/username?value

    /*
     * Giving the user some visual feedback about transfer of ownership.
     */
    micro.windows.info:File's ownership was transferred to you
      class:micro-windows-info success

  /*
   * Displaying widget to user.
   */
  eval-x:x:/+/**/_id|/+/*/*/*/*(/value|/innerValue)
  create-widgets
    micro.widgets.modal:sulphur-file-editor-wrapper
      widgets

        /*
         * File name description header
         */
        h3
          innerValue:x:/@p5.mysql.select/*/*/filename?value

        /*
         * Name textbox.
         */
        input:sulphur-file-name-textbox
          type:text
          class:fill
          value:x:/@p5.mysql.select/*/*/name?value
          placeholder:Name of file ...
          title:Name for file
          onkeydown:@"if (event.keyCode == 13) {p5.$('sulphur-file-name-textbox').raise('onchange');p5.$('sulphur-close-file-editor').raise('onclick');return false;}"
          oninit

            /*
             * Setting initial focus to "name" textbox.
             */
            micro.page.set-focus:x:/../*/_event?value

          onchange

            /*
             * Updates file's name in database.
             */
            _id:x:/@p5.mysql.select/*/*/id?value
            get-widget-property:x:/../*/_event?value
              value
            p5.mysql.connect:[sulphur]
              p5.mysql.update:@"update files set name = @name where id = @id"
                @name:x:/@get-widget-property/*/*?value
                @id:x:/@_id?value

        /*
         * Description textarea widget.
         */
        textarea
          placeholder:Description of file ...
          class:fill
          innerValue:x:/@p5.mysql.select/*/*/description?value
          rows:7
          title:Description for file, feel free to use Markdown here
          style:"margin-bottom:3rem;"
          onchange

            /*
             * Updates file's description in database.
             */
            _id:x:/@p5.mysql.select/*/*/id?value
            get-widget-property:x:/../*/_event?value
              value
            p5.mysql.connect:[sulphur]
              p5.mysql.update:@"update files set description = @description where id = @id"
                @description:x:/@get-widget-property/*/*?value
                @id:x:/@_id?value

        /*
         * Protection for file wrapper widget.
         */
        div
          style:"margin-bottom:2rem;margin-top:-2rem;"
          oninit

            /*
             * Checking file's protection, and setting radio buttons
             * initial values accordingly.
             *
             * Notice, in case guest uploads are enabled, and user is a guest,
             * we entirely delete this widget.
             *
             * This allows for an "anonymous WikiLeaks type of DropBox".
             */
            whoami
            if:x:/-/*/default?value

              /*
               * User is guest, deleting widget and returning early.
               */
              delete-widget:x:/../*/_event?value
              return

            _id:x:/@p5.mysql.select/*/*/id?value
            p5.mysql.connect:[sulphur]
              p5.mysql.scalar:@"select type from files where id = @id"
                @id:x:/@_id?value
              switch:x:/@p5.mysql.scalar?value
                case:private
                  set-widget-property:sulphur-rdo-private
                    checked
                case:protected
                  set-widget-property:sulphur-rdo-protected
                    checked
                case:public
                  set-widget-property:sulphur-rdo-public
                    checked

          events

            /*
             * Changes file's accessibility.
             *
             * Expected [protection] being either "private", "protected" or "public".
             */
            sulphur.edit-file.set-protection

              /*
               * Sanity check.
               */
              micro.lambda.contract.min:x:/..
                protection:string

              /*
               * updating file's protection in database.
               */
              _id:x:/@p5.mysql.select/*/*/id?value
              p5.mysql.connect:[sulphur]
                p5.mysql.select:@"select * from files where id = @id"
                  @id:x:/@_id?value
                p5.mysql.update:@"update files set type = @protection where id = @id"
                  @protection:x:/../*/protection?value
                  @id:x:/@_id?value

              /*
               * Physically moving file on disc.
               *
               * Notice, we need to figure out where the file previously physically
               * was on disc, which depends upon file's previous protection level.
               * For then to figure out file's new location, which depends upon
               * its new protection level.
               */
              _src
              _dest
              switch:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/type?value
                case:private
                  set:x:/@_src?value
                    src:~/documents/private/{0}{1}
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
                case:protected
                  set:x:/@_src?value
                    src:/common/documents/private/{0}{1}
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
                case:public
                  set:x:/@_src?value
                    src:/common/documents/public/{0}{1}
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
              switch:x:/../*/protection?value
                case:private
                  set:x:/@_dest?value
                    src:~/documents/private/{0}{1}
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
                case:protected
                  set:x:/@_dest?value
                    src:/common/documents/private/{0}{1}
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value
                case:public
                  set:x:/@_dest?value
                    src:/common/documents/public/{0}{1}
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/prefix?value
                      :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/filename?value

              /*
               * Now we know file's previous location, in addition to its
               * new location, and can move onwards with physically moving 
               * the file on disc.
               */
              move-file:x:/@_src?value
                dest:x:/@_dest?value

          widgets
            p
              innerValue:@"Public means accessible to anyone, protected means only accessible for registered users, and private means only accessible for the file's owner."
            input:sulphur-rdo-private
              type:radio
              name:sulphur-rdo-protection
              onchange

                /*
                 * Doing database update.
                 */
                sulphur.edit-file.set-protection
                  protection:private

            label
              for:sulphur-rdo-private
              innerValue:Private

            input:sulphur-rdo-protected
              type:radio
              name:sulphur-rdo-protection
              onchange

                /*
                 * Doing database update.
                 */
                sulphur.edit-file.set-protection
                  protection:protected

            label
              for:sulphur-rdo-protected
              innerValue:Protected

            input:sulphur-rdo-public
              type:radio
              name:sulphur-rdo-protection
              onchange

                /*
                 * Doing database update.
                 */
                sulphur.edit-file.set-protection
                  protection:public

            label
              for:sulphur-rdo-public
              innerValue:Public

        /*
         * Close button.
         */
        a:sulphur-close-file-editor
          href:#
          role:button
          class:sulphur-close-file-editor
          innerValue:@"<span class=""icon-close""></span>"
          title:Close editor ...
          onclick

            /*
             * Re-databinding the edited file's widget, and deleting edit widget.
             */
            _id:x:/@p5.mysql.select/*/*/id?value
            sulphur.files.update:x:/@_id?value
            delete-widget:sulphur-file-editor-wrapper

        /*
         * Delete file button.
         */
        a
          href:#
          class:sulphur-delete-file-editor
          innerValue:@"<span class=""icon-trash""></span>"
          title:Delete file ...
          onclick

            /*
             * Deleting file and redirecting to main app's root URL.
             */
            _id:x:/@p5.mysql.select/*/*/id?value
            p5.mysql.connect:[sulphur]
              p5.mysql.transaction.begin
                p5.mysql.select:@"select username, prefix, filename, type from files where id = @id"
                  @id:x:/@_id?value
                p5.mysql.delete:@"delete from files where id = @id"
                  @id:x:/@_id?value

                /*
                 * Figuring out file's folder.
                 */
                _folder
                if:x:/@p5.mysql.select/*/*/username?value
                  =:guest
                  and:x:/@p5.mysql.select/*/*/type?value
                    =:private

                  /*
                   * Privately owned guest file.
                   */
                  set:x:/@_folder?value
                    src:/common/documents/private/

                else-if:x:/@p5.mysql.select/*/*/type?value
                  =:private

                  /*
                   * Privately owned user file.
                   */
                  set:x:/@_folder?value
                    src:/users/{0}/documents/private/
                      :x:/@p5.mysql.select/*/*/username?value

                else-if:x:/@p5.mysql.select/*/*/type?value
                  =:protected

                  /*
                   * Protected user file.
                   */
                  set:x:/@_folder?value
                    src:/common/documents/private/

                else-if:x:/@p5.mysql.select/*/*/type?value
                  =:public

                  /*
                   * Public user file.
                   */
                  set:x:/@_folder?value
                    src:/common/documents/public/

                delete-file:{0}{1}{2}
                  :x:/@_folder?value
                  :x:/@p5.mysql.select/*/*/prefix?value
                  :x:/@p5.mysql.select/*/*/filename?value

                /*
                 * Committing transaction.
                 */
                p5.mysql.transaction.commit

                /*
                 * Deleting main file editor.
                 */
                delete-widget:sulphur-file-editor-wrapper

                /*
                 * Making sure we delete the widget wrapping the current file
                 * from our "grid".
                 */
                sulphur.files.delete:x:/@_id?value
