
/*
 * Invoked when a file is uploaded to the server.
 *
 * Expects the HTTP request to be of type "multipart/form-data" and that the file
 * can be found as HTTP POST parameter "file".
 *
 * Will return the database ID of the uploaded file if successful, otherwise it
 * will throw an exception.
 */





/*
 * Verifying user is allowed to upload files.
 */
whoami
select-data:x:/*/*/sulphur.settings
if:x:/@select-data/*/*/uploaders?value
  =:all
  or:x:/@select-data/*/*/uploaders?value
    =:users
    and:x:/@whoami/*/default?value
      !=:bool:true
  or:x:/@select-data/*/*/uploaders?value
    =:root
    and:x:/@whoami/*/role?value
      =:root

  /*
   * Somehow, user is allowed to upload files to server.
   *
   * Retrieving file's content, and saving it to correct folder.
   */
  p5.web.request.parse-mime
    attachment-folder:~/documents/private/

  /*
   * Figuring out SHA value of file, to avoid inserting same file multiple times.
   */
  p5.crypto.hash.create-sha1-file:{0}{1}{2}
    :x:/@p5.web.request.parse-mime/**/filename/[0,1]/*/folder?value
    :x:/@p5.web.request.parse-mime/**/filename/[0,1]/*/prefix?value
    :x:/@p5.web.request.parse-mime/**/filename/[0,1]?value
    hex:true

  /*
   * Inserting entry into database.
   */
  p5.mysql.connect:[sulphur]

    /*
     * Checking if there exists a file with the same SHA value from before, 
     * at which point we don't insert the file.
     */
    p5.mysql.scalar:@"select count(*) from files where sha = @sha"
      @sha:x:/@p5.crypto.hash.create-sha1-file?value
    if:x:/@p5.mysql.scalar?value.int
      !=:int:0

      /*
       * File already exists in system.
       */
      delete-file:{0}{1}{2}
        :x:/@p5.web.request.parse-mime/**/filename/[0,1]/*/folder?value
        :x:/@p5.web.request.parse-mime/**/filename/[0,1]/*/prefix?value
        :x:/@p5.web.request.parse-mime/**/filename/[0,1]?value
      throw:Sorry, we have that file from before

    /*
     * Doing the actual insertion of file.
     */
    p5.mysql.insert:@"insert into files (filename, prefix, username, name, description, type, sha) values(@filename, @prefix, @username, @name, @description, 'private', @sha)"
      @filename:x:/@p5.web.request.parse-mime/**/filename/[0,1]?value
      @prefix:x:/@p5.web.request.parse-mime/**/filename/[0,1]/*/prefix?value
      @username:x:/@whoami/*/username?value
      @name:x:/@p5.web.request.parse-mime/**/filename/[0,1]?value
      @description:
      @sha:x:/@p5.crypto.hash.create-sha1-file?value

    /*
     * Return database ID of newly uploaded file.
     */
    return:x:/@p5.mysql.insert/*/id?value

else

  /*
   * User is not allowed to upload file, throwing an exception with some basic
   * information about why.
   */
  throw:Access denied!
