
/*
 * Launcher file for Sulphur Five.
 *
 * This file is evaluated when Sulphur is launched by the user, with [url] being
 * the URL relative to P5 main folder.
 */





/*
 * Splitting URL, to figure out which type of request this is.
 */
split:x:/../*/url?value
  =:/





/*
 * Checking if this is an upload request, and if so, delegating work to uploader,
 * and returning early.
 */
if:x:/@split/1?name
  =:upload

  /*
   * This is an upload request, delegating work to file responsible for handling our upload,
   * making sure we return the ID of file to client, if it was successfully uploaded.
   */
  try

    micro.evaluate.file:@SULPHUR/handle-files/upload-file.hl
    p5.web.echo:SUCCESS

  catch

    /*
     * Notice, we do not return the exception message, since that would allow an adversary to
     * understand what goes wrong, which would increase the attack surface, and compromise the
     * security of the system.
     */
    p5.web.echo:ERROR

  /*
   * Returning early after upload has been handled.
   */
  return





/*
 * Checking if this is a request to download a file, at which point we initiate a file
 * download request, and return early.
 */
if:x:/@split/1?name
  =:download

  /*
   * This is a download request, serving file and returning early.
   */
  eval-x:x:/+/*
  micro.evaluate.file:@SULPHUR/handle-files/download-file.hl
    sha:x:/@split/2?name
  return





/*
 * Creating main content container.
 *
 * This is the place where "everything" is stored in Sulphur, and is used to display
 * the list of files, upload button, etc.
 *
 * Notice, we only use one "container" widget from Micro for everything related to Sulphur.
 */
create-container-widget:sulphur-main-container
  class:container





/*
 * Including Micro, adding Awesome Fonts, serious skin, in addition to Sulphur's
 * main CSS file.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/ext.css
p5.web.include-css-file:@MICRO/media/fonts.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:@SULPHUR/media/main.css





/*
 * Evaluating file responsible for creating actual viewport.
 */
micro.evaluate.file:@SULPHUR/main/create-display.hl
