
/*
 * Launcher file for Sulphur Five.
 *
 * This file is evaluated (without any parameters) when the desktop icon for 
 * Sulphur is clicked by the user.
 */





/*
 * Clearing out anything previously added to page, to make sure we start
 * out Sephia with a blank slate.
 */
clear-widget:cnt
delete-widget-property:cnt
  class





/*
 * Creating main content container.
 *
 * This is the place where "everything" is stored in Sephia, and is used to display
 * the inbox, Sephia's settings, emails the user is reading, etc, etc, etc.
 *
 * Notice, we only use one "container" widget from Micro for everything related to Sephia.
 */
create-container-widget:sulphur-main-container
  class:container





/*
 * Including Micro, and adding Awesome Fonts.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/ext.css
p5.web.include-css-file:@MICRO/media/fonts.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:@SULPHUR/media/main.css





/*
 * Verifying user is logged in, and if not, forcing the user to login before we proceed.
 */
whoami
if:x:/@whoami/*/default?value

  /*
   * User is not logged in.
   */
  p5.core.login

  /*
   * Returning early to abort evaluating the rest of our file.
   */
  return





/*
 * Checking if this is a request for a file, at which point we return it to client,
 * and return early.
 */
p5.web.query.get:file
if:x:/-/*?value

  /*
   * This is a file request, first figuring out HTTP headers to set according
   * to file's extension.
   */
  split:x:/@p5.web.query.get/*?value
    =:.
  to-lower:x:/@split/0/-?name
  load-file:@SULPHUR/configuration/mime-types.hl

  /*
   * Setting "Content-Type" header.
   */
  p5.web.header.set:Content-Type
    src:{0}/{1}
      :x:/@load-file/*/*/{0}/*?name
        :x:/@to-lower?value
      :x:/@load-file/*/*/{0}/*?value
        :x:/@to-lower?value

  /*
   * Looping through all other HTTP headers according to configuration.
   */
  for-each:x:/@load-file/*/*/{0}/*/*
    :x:/@to-lower?value
    p5.web.header.set:x:/@_dp/#?name
      src:x:/@_dp/#?value

  /*
   * Echoing file to caller.
   */
  p5.web.echo-file:x:/@p5.web.query.get/*?value
  return





/*
 * Evaluating file responsible for creating actual viewport.
 */
micro.evaluate.file:@SULPHUR/create-display.hl
