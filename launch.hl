
/*
 * Launcher file for Sulphur Five.
 *
 * This file is evaluated (without any parameters) when Sulphur is launched by the user.
 */





/*
 * Checking if this is an upload request, and if so, delegating work to uploader.
 */
p5.io.unroll-path:@SULPHUR/
split:x:/-?value
  =:/
if:x:/../*/url?value
  =:/{0}/upload
    :x:/@split/0/-?name

  /*
   * This is an upload request, delegating work to file responsible for handling it,
   * making sure we return the ID of file to client, if it was successfully uploaded.
   */
  try

    micro.evaluate.file:@SULPHUR/handle-files/upload-file.hl
    p5.web.echo:x:/-?value

  catch

    /*
     * Notice, we do not return the exception message, since that would allow an adversary to
     * understand what goes wrong, which would widen the attack surface, and compromise the
     * security of the system!
     */
    p5.web.echo:ERROR

  return





/*
 * Creating main content container.
 *
 * This is the place where "everything" is stored in Sulphur, and is used to display
 * the files list. upload button, etc.
 *
 * Notice, we only use one "container" widget from Micro for everything related to Sulphur.
 */
create-container-widget:sulphur-main-container
  class:container





/*
 * Including Micro, and adding Awesome Fonts, and serious skin.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/ext.css
p5.web.include-css-file:@MICRO/media/fonts.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:@SULPHUR/media/main.css





/*
 * Verifying user is logged in, or that app is available for everyone,
 * including guest accounts.
 */
whoami
select-data:x:/*/*/sulphur.settings
if:x:/@whoami/*/default?value
  and:x:/@select-data/*/*/viewers?value
    !=:guest

  /*
   * User is not logged in, and app is restricted to only logged in users.
   */
  p5.core.login

  /*
   * Returning early to abort evaluating the rest of our file.
   */
  return





/*
 * Checking if this is a request for a file, at which point we initiate a file
 * download request, and return early.
 */
p5.web.query.get:download
if:x:/-/*?value

  /*
   * This is a download request, serving file and returning early.
   */
  eval-x:x:/+/*
  micro.evaluate.file:@SULPHUR/handle-files/download-file.hl
    id:x:/@p5.web.query.get/*?value
  return





/*
 * Evaluating file responsible for creating actual viewport.
 */
micro.evaluate.file:@SULPHUR/main/create-display.hl
