
/*
 * File responsible for databinding files in the main grid.
 *
 * Requires no arguments, but you can optionally pass in [offset] and [filter] when invoking file,
 * to start out at a specific page, with a specified filter condition.
 *
 * Notice, optionally pass in [more] and set its value to the database ID of from
 * where you wish to start fetching more items from, and rather "append" to grid,
 * instead of clearing old items, and re-populating grid.
 */
.defaults
  offset:int:0





/*
 * Sanity checking optional arguments.
 */
micro.lambda.contract.optional:x:/..
  offset:long
  filter:string





/*
 * Checking if a filter was provided, and if so, adding a "where" clause to 
 * our SQL statement.
 */
_filter
if:x:/../*/filter?value

  /*
   * Filter was supplied, making sure we use it.
   */
  set:x:/@_filter?value
    src:"and (username = @exact or name like @filter or description like @filter or filename like @filter)"
  eval-x:x:/+/*/*
  add:x:/../*/p5.mysql.connect/*/p5.mysql.select
    src
      @filter:"%{0}%"
        :x:/../*/filter?value
      @exact:"{0}"
        :x:/../*/filter?value





/*
 * Making sure we allow only for viewing files that the user actually has access to.
 *
 * Notice, a user has access to all of his own files, in addition to all public
 * and protected files.
 *
 * The exception is a "guest", which only has access to public files.
 */
whoami
_where
if:x:/@whoami/*/default?value

  /*
   * User is guest, making sure he only has access to public files.
   */
  set:x:/@_where?value
    src:"type = 'public'"

else

  /*
   * User is not guest, but an actual logged in user.
   *
   * Hence giving him access to also "protected" files, in addition to all of user's
   * own files, and all of guest's files, including guest's private files.
   */
  set:x:/@_where?value
    src:"(username = @username or username = 'guest' or type = 'protected' or type = 'public')"
  add:x:/../*/p5.mysql.connect/*/p5.mysql.select
    src
      @username:x:/@whoami/*/username?value





/*
 * Checking if caller requested [more] items, instead of re-binding grid.
 */
_more
if:x:/../*/more?value
  set:x:/@_more?value
    src:"and id < @more"
  add:x:/../*/p5.mysql.connect/*/p5.mysql.select
    src
      @more:x:/../*/more?value





/*
 * Connecting to database and selecting available files for user.
 */
p5.mysql.connect:[sulphur]
  p5.mysql.select:@"select * from files where {0} {1} {2} order by id desc limit 10 offset @offset"
    :x:/@_where?value
    :x:/@_filter?value
    :x:/@_more?value
    @offset:x:(/../*/offset|/../*/.defaults/*/offset)/$?value.int

  /*
   * Storing the ID of the last record we fetched in ViewState, in case there
   * comes another request for "more" items later, at which point we extract
   * the last ID, and select files from that point and onwards.
   */
  p5.web.viewstate.set:sulphur.last-file-id
    src:x:/@p5.mysql.select/0/-/*/id?value

  /*
   * Making sure we display "end of dataset ruler" if we are at end of dataset,
   * and it doesn't exist from before.
   */
  if:x:/@p5.mysql.select/*?count.int
    <:int:10
    and
      fetch:x:/0/0?value
        widget-exists:sulphur-at-end-of-dataset
      not

    /*
     * "End of dataset ruler" does not exist, and we are at the end of our dataset,
     * hence creating a visual clue, signaling that we're at the end of dataset,
     * in addition to signaling to JavaScript logic, that it shouldn't try to 
     * fetch more items.
     */
    create-widget:sulphur-at-end-of-dataset
      parent:sulphur-main-container
      element:hr
    p5.web.send-javascript:"p5.sulphur_end_of_dataset = true;"

  else-if:x:/@p5.mysql.select/*?count.int
    >=:int:10
    and
      fetch:x:/0/0?value
        widget-exists:sulphur-at-end-of-dataset

    /*
     * "End of dataset ruler" exists, and there are possibly more items,
     * hence we delete our visual clue, and signal to our JavaScript "feeder"
     * logic that it should attempt to retrieve more items when user is at bottom
     * of page.
     */
    delete-widget:sulphur-at-end-of-dataset
    p5.web.send-javascript:"p5.sulphur_end_of_dataset = false;"

  /*
   * Looping through each result from above, and creating one widget for each result.
   */
  for-each:x:/@p5.mysql.select/*

    /*
     * Making sure we transform the description of file from Markdown to HTML.
     *
     * TODO: Validate HTML for Markdown HTML malicious tag injection!
     */
    markdown2html:x:/@_dp/#/*/description?value

    /*
     * Nicely formatting date.
     */
    p5.types.date.format:x:/@_dp/#/*/uploaded?value
      format:"ddd d. MMM yyyy, HH:mm"

    /*
     * Figuring out file size.
     */
    _folder
    if:x:/@_dp/#/*/username?value
      =:guest

      /*
       * Guests can only have private files.
       */
      set:x:/@_folder?value
        src:/common/documents/private/

    else

      /*
       * Need to figure out type of file.
       */
      switch:x:/@_dp/#/*/type?value
        case:private
          set:x:/@_folder?value
            src:/users/{0}/documents/private/
              :x:/@_dp/#/*/username?value
        case:protected
          set:x:/@_folder?value
            src:/common/documents/private/
        case:public
          set:x:/@_folder?value
            src:/common/documents/public/

    /*
     * Now we know which folder file is in, and we can retrieve its size.
     */
    _length
    p5.io.file.get-length:{0}{1}{2}
      :x:/@_folder?value
      :x:/@_dp/#/*/prefix?value
      :x:/@_dp/#/*/filename?value

    /*
     * Dividing size on 1024 to figure out KB size of file.
     */
    set:x:/@_length?value
      /:x:/@p5.io.file.get-length/*?value.decimal
        _:decimal:1024

    /*
     * Forward evaluating [add] invocations arguments, and adding a widget into
     * [create-widgets] invocation below, for creating a widget wrapping file information.
     */
    eval-x:x:/+/*/*/*/widgets/*/container/*/widgets/*(/h3|/label|/div)/*/innerValue|/+/**/_id
    add:x:/..p5.mysql.connect/*/create-widgets
      src

        /*
         * Actual wrapper for entire file widget.
         */
        container
          parent:sulphur-files-grid
          class:col-100
          widgets
            container
              class:sulphur-file-item rounded shaded air-inner
              widgets

                /*
                 * Displays name of file.
                 *
                 * Not filename, but friendly "header" for file that is.
                 */
                h3
                  innerValue:x:/@_dp/#/*/name?value

                /*
                 * Meta information, username, protection, and upload date.
                 */
                label
                  class:sulphur-meta
                  innerValue:"{0:n0}KB<br/>{1} ({2})<br/>{3}<br/>{4}"
                    :x:/@_length?value
                    :x:/@_dp/#/*/username?value
                    :x:/@_dp/#/*/type?value
                    :x:/@p5.types.date.format?value
                    :x:/@_dp/#/*/filename?value

                /*
                 * Download file hyperlink.
                 */
                a
                  class:sulphur-download-file
                  innerValue:@"<span class=""icon-download""></span>"
                  title:Download file ...
                  oninit

                    /*
                     * Changing [href] of widget to allow downloading of file.
                     */
                    _id:x:/@_dp/#/*/id?value
                    p5.web.get-location-url
                    set-widget-property:x:/../*/_event?value
                      href:{0}?download={1}
                        :x:/@p5.web.get-location-url?value
                        :x:/@_id?value


                /*
                 * Edit file widget, only visible if user is root, or file belongs
                 * to currently logged in user.
                 */
                a
                  class:sulphur-edit-file
                  innerValue:@"<span class=""icon-pencil""></span>"
                  title:Edit file ...
                  oninit

                    /*
                     * Checking if user is guest, and if so, deleting entire widget
                     * to avoid having guests editing files after having (optionally)
                     * initially uploaded them.
                     */
                    whoami
                    if:x:/-/*/default?value

                      /*
                       * User is guest.
                       */
                      delete-widget:x:/../*/_event?value
                      return

                    /*
                     * Forward evaluated above.
                     */
                    _id:x:/@_dp/#/*/id?value

                    /*
                     * Verifying user is allowed to editing file, and if not,
                     * simply removing widget.
                     */
                    whoami
                    if:x:/@whoami/*/default?value
                      =:guest

                      /*
                       * Guests are never allowed to edit files.
                       */
                      delete-widget:x:/../*/_event?value
                      return

                    if:x:/@whoami/*/role?value
                      !=:root

                      /*
                       * User is not root, therefor we must check if user is the
                       * owner of the file, and if not, we don't allow him to edit
                       * the file.
                       */
                      p5.mysql.connect:[sulphur]
                        p5.mysql.scalar:@"select username from files where id = @id"
                          @id:x:/@_id?value
                        if:x:/@p5.mysql.scalar?value
                          !=:x:/@whoami/*/username?value

                          /*
                           * User does not own file, deleting widget, and returning
                           * early.
                           */
                          delete-widget:x:/../*/_event?value
                          return

                    /*
                     * Changing [href] of widget to allow for editing the file.
                     */
                    p5.web.get-location-url
                    set-widget-property:x:/../*/_event?value
                      href:{0}?edit={1}
                        :x:/@p5.web.get-location-url?value
                        :x:/@_id?value

                /*
                 * Description for file.
                 */
                div
                  innerValue:x:/@markdown2html?value

  /*
   * Clearing widget's old content, and re-databinding files grid.
   *
   * Notice, we only clear old content if [keep-old] is not given, or has a false
   * value.
   */
  if:x:/../*/more?value
    not

    /*
     * No [more] argument, clearing out old files from grid.
     */
    clear-widget:sulphur-files-grid

  /*
   * Creating widgets for each file.
   */
  create-widgets
