
/*
 * Converts the given [_arg] HTML snippet's URLs and tags into hyperlinks, 
 * and returns the result to the caller.
 */
create-event:sulphur._internals.html.create-hyperlinks-and-tags

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    _arg:string

  /*
   * Fetching app's root URL.
   */
  p5.web.get-root-location
  p5.io.unroll-path:@SULPHUR/
  split:x:/-?value
    =:/

  /*
   * Replacing all "#xxx" occurrencies in content, into a relative URL, leading
   * to "/tag/xxx"
   */
  replace:x:/../*/_arg?value
    src:regex:@"/(\B)(#(\w[-\w]+))/i"
    dest:"$1<a href='{0}{1}/tag/$3'>$2</a>"
      :x:/@p5.web.get-root-location?value
      :x:/@split/0/-?name

  /*
   * Using regular expression to convert anything that resembles a hyperlink, that
   * has not already been converted somehow, into an actual clickable hyperlink, making
   * sure we append a "target" attribute, leading to "_blank".
   */
  replace:x:/@replace?value
    src:regex:@"/(\(|<|\[|\s|>)+(http|https|ftp)(\://)([a-zA-Z0-9\-\.]+\.[a-zA-Z]{2,3})((:[a-zA-Z0-9]*)?/?([a-zA-Z0-9\-\._\?\,\'/\\\+&amp;%\$#\=~])*)/"
    dest:"$1<a target='_blank' href='$2$3$4$5'><strong>$4</strong>$5</a>"

  /*
   * Returning the results of the above regular expression replacement.
   */
  return:x:/@replace?value
